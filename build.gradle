plugins {
  id 'java'
  id 'org.jetbrains.intellij.platform' version '2.0.1'
}

group 'com.github.tarn2206'
version '2.0.0'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()

  intellijPlatform {
    defaultRepositories()
  }
}

dependencies {
  intellijPlatform {
    intellijIdeaCommunity '2023.1'
    bundledPlugins("com.intellij.java", "com.intellij.gradle", "org.jetbrains.android")
    instrumentationTools()
    pluginVerifier()
  }
  implementation gradleApi()
  implementation 'io.reactivex.rxjava3:rxjava:3.1.9'
  implementation 'com.github.akarnokd:rxjava3-swing:3.1.1'
  compileOnly 'org.projectlombok:lombok:1.18.34'
  annotationProcessor 'org.projectlombok:lombok:1.18.34'
  testImplementation 'junit:junit:4.13.2'
}

import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.intellij.platform.gradle.models.ProductRelease

def sinceBuildNumber = '231'
def untilBuildNumber = '242.*'

// https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin-extension.html#intellijPlatform-pluginVerification-ides
intellijPlatform {
  pluginVerification {
    ides {
      recommended()
      select {
        it.types = [IntelliJPlatformType.IntellijIdeaCommunity]
        it.channels = [ProductRelease.Channel.RELEASE]
        it.sinceBuild = "${sinceBuildNumber}"
        it.untilBuild = "${untilBuildNumber}"
      }
    }
  }
}

buildSearchableOptions.enabled = false

// https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html#intellij-platform-based-products-of-recent-ide-versions
patchPluginXml {
  sinceBuild = "${sinceBuildNumber}"
  untilBuild = "${untilBuildNumber}"
  changeNotes = """
<h2>2.0.0</h2>
<ul>
    <li>Upgrade to IntelliJ Platform Gradle Plugin (2.x)</li>
</ul>
<h2>1.6.4</h2>
<ul>
    <li>Remove deprecated and internal api usage</li>
</ul>
<h2>1.6.3</h2>
<ul>
    <li>Support authenticate private maven repository by include credentials in the URL<br>https://username:password@your-repo.com/</li>
    <li>Improve Android project support</li>
</ul>
<h2>1.6.2</h2>
<ul>
    <li>Fix tool window title</li>
</ul>
<h2>1.6.1</h2>
<ul>
    <li>Fix not detect android test implementation</li>
    <li>Resolve conflict tool window name</li>
</ul>
<h2>1.6</h2>
<ul>
    <li>Support custom maven repository</li>
    <li>Filtering unstable versions</li>
</ul>
<h2>1.5.2</h2>
<ul>
    <li>Move refresh action from popup menu to tool window title</li>
    <li>Remove close button from tool window title</li>
</ul>
<h2>1.5.1</h2>
<ul>
    <li>Support a prefix version range: e.g. 1.+, 1.3.+</li>
    <li>Fix bug android projects not show dependencies</li>
</ul>
<h2>1.5</h2>
<ul>
    <li>Support nested-project</li>
    <li>Prevent multiple check update for same dependency</li>
    <li>Resolve existing tool window name</li>
</ul>
<h2>1.4</h2>
<ul>
    <li>Support multi-project build</li>
    <li>Remove require dependencies plugins</li>
</ul>
"""
}
